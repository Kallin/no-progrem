apply from: "gradle/idea.gradle"
apply from: "gradle/dependencies.gradle"

allprojects {
    repositories {
        mavenCentral()
    }

    if (project.projectDir =~ '.groovy') {
        apply plugin: 'groovy'
        apply plugin: 'codenarc'
    }

    if (project.projectDir =~ '.java') {
        apply plugin: 'java'
        apply plugin: 'checkstyle'

        checkstyle.configFile = file "$rootProject.projectDir/config/checkstyle/checkstyle.xml"
    }

    if (project.projectDir =~ '.scala') {
        apply plugin: 'scala'
    }

    plugins.withType(JavaPlugin.class) {
        dependencies {
            compile deps.slf
            testCompile deps.junit
        }
    }

    plugins.withType(GroovyPlugin.class) {
        codenarc.configFile = file "$rootProject.projectDir/config/codenarc/rules.groovy"

        dependencies {
            compile deps.slf, deps.groovy
            testCompile deps.hamcrest, deps.spock
        }
    }

    plugins.withType(ScalaPlugin.class) {
        dependencies {
            testCompile deps.scalaTest
        }
    }
}